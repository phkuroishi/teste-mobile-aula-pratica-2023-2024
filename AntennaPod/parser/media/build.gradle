plugins {
    id("com.android.library")
}
apply from: "../../common.gradle"

android {
    namespace "de.danoeh.antennapod.parser.media"
}

dependencies {
    implementation project(':model')

    annotationProcessor "androidx.annotation:annotation:$annotationVersion"

    implementation "commons-io:commons-io:$commonsioVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
}

jacoco {
    toolVersion '0.8.7'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "skipped", "passed", "failed"
        showStandardStreams true
        displayGranularity 2
    }
    jacoco {
        excludes = ['jdk.internal.*']
        includeNoLocationClasses = true
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*']
    def debugTree = fileTree(dir: "$buildDir/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "$projectDir/src/main/java"


    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec"
    ]))
}