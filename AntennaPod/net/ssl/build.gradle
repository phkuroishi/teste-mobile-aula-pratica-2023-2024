plugins {
    id("com.android.library")
}
apply from: "../../common.gradle"
apply from: "../../playFlavor.gradle"

android {
    namespace "de.danoeh.antennapod.net.ssl"
}

dependencies {
    annotationProcessor "androidx.annotation:annotation:$annotationVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"

    testImplementation "junit:junit:$junitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    playImplementation 'com.google.android.gms:play-services-base:17.5.0'
    // This version should be updated regularly.
    freeImplementation 'org.conscrypt:conscrypt-android:2.5.2'
}

jacoco {
    toolVersion '0.8.7'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "skipped", "passed", "failed"
        showStandardStreams true
        displayGranularity 2
    }
    jacoco {
        excludes = ['jdk.internal.*']
        includeNoLocationClasses = true
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testFreeDebugUnitTest']) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*']
    def debugTree = fileTree(dir: "$buildDir/intermediates/javac/freeDebug", excludes: fileFilter)
    def mainSrc = "$projectDir/src/main/java"


    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "outputs/unit_test_code_coverage/freeDebugUnitTest/testFreeDebugUnitTest.exec"
    ]))
}